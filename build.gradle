plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.18.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'io.github.arc-blroth'
archivesBaseName = 'maow2am'

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'

    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = 8
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    from 'LICENSE'
}

pluginBundle {
    website = "https://$pluginUrlBase"
    vcsUrl = "https://${pluginUrlBase}.git"
    tags = ['cat']
}

gradlePlugin {
    plugins {
        maow2am {
            id = 'io.github.arc-blroth.maow2am'
            implementationClass = 'ai.arcblroth.maow2am.Maow2AMPlugin'
            displayName = pluginDisplayName
            description = pluginDescription
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            forEach {
                if(it instanceof MavenPublication) {
                    it.pom {
                        url = "https://$pluginUrlBase"
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'arcblroth'
                                name = "Arc'blroth"
                            }
                        }
                        scm {
                            def scmRepo = "scm:git:git://${pluginUrlBase}.git"
                            connection = scmRepo
                            developerConnection = scmRepo
                            url = scmRepo
                        }
                    }
                }
            }
        }
    }
}
